;;; Waxeye Parser Generator
;;; www.waxeye.org
;;; Copyright (C) 2008 Orlando D. A. R. Hill
;;;
;;; Permission is hereby granted, free of charge, to any person obtaining a copy of
;;; this software and associated documentation files (the "Software"), to deal in
;;; the Software without restriction, including without limitation the rights to
;;; use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies
;;; of the Software, and to permit persons to whom the Software is furnished to do
;;; so, subject to the following conditions:
;;;
;;; The above copyright notice and this permission notice shall be included in all
;;; copies or substantial portions of the Software.
;;;
;;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
;;; IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
;;; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
;;; AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
;;; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
;;; OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
;;; SOFTWARE.


(module
scheme
mzscheme

(require (lib "ast.ss" "waxeye")
         (lib "fa.ss" "waxeye")
         "code.scm" "dfa.scm" "gen.scm" "version.scm")
(provide gen-scheme gen-scheme-parser code-scheme-comment-base)


(define (gen-scheme grammar path)
  (code-indent-unit! " ")
  (gen-scheme-parser grammar)
  (dump-output (string-append path (if *name-prefix*
                                       (string-append *name-prefix* "-parser.scm")
                                       "parser.scm")))
  (clear-output))


(define (code-scheme-comment-base unit lines)
  (for-each (lambda (a)
              (if (equal? "" a)
                  (code-isn unit)
                  (code-pisn unit " " a)))
            lines))


(define (code-scheme-comment . lines)
  (code-scheme-comment-base ";;" lines))


(define (code-scheme-header-comment . lines)
  (code-scheme-comment-base ";;;" lines))


(define (gen-scheme-trans a)
  (define (gen-list-item a)
    (if (char? a)
        (code-s (format "~s" a))
        (code-paren
         (code-s "cons ")
         (code-s (format "~s" (car a)))
         (code-s " ")
         (code-s (format "~s" (cdr a))))))
  (cond
   ((symbol? a) (code-s (format "'~s" a)))
   ((list? a)
    (code-s "(list ")
    (gen-list-item (car a))
    (for-each (lambda (b)
                (code-s " ")
                (gen-list-item b))
              (cdr a))
    (code-s ")"))
   (else (code-s (format "~s" a)))))


(define (gen-scheme-edge a)
  (code-n)
  (code-i)
  (code-paren
   (code-s "make-edge")
   (code-s " ")
   (gen-scheme-trans (edge-t a))
   (code-s " ")
   (code-s (edge-s a))
   (code-s " ")
   (code-s (edge-v a))))


(define (gen-scheme-edges edges)
  (code-iu
   (code-paren
    (code-s "list")
    (for-each gen-scheme-edge edges))))


(define (gen-scheme-state a)
  (code-n)
  (code-i)
  (code-paren
   (code-s "make-state")
   (code-s " ")
   (gen-scheme-edges (state-edges a))
   (code-s " ")
   (code-s (state-match a))))


(define (gen-scheme-states states)
  (code-iu
   (code-paren
    (code-s "vector")
    (for-each gen-scheme-state (vector->list states)))))


(define (gen-scheme-fa a)
  (code-n)
  (code-i)
  (code-paren
   (code-s "make-fa")
   (code-s " '")
   (code-s (camel-case-lower (symbol->string (fa-type a))))
   (code-s " ")
   (gen-scheme-states (fa-states a))
   (code-s " '")
   (code-s (fa-mode a))))


(define (gen-scheme-parser grammar)
  (let ((parser-name (if *name-prefix*
                         (string-append *name-prefix* "-parser")
                         "parser")))
    (if *file-header*
        (map code-scheme-header-comment *file-header*)
        (code-scheme-header-comment (string-append "Generated by the Waxeye Parser Generator - version " *version*) "www.waxeye.org"))
    (code-n)
    (code-sn "(module")
    (code-sn parser-name)
    (code-sn "mzscheme")
    (code-n)
    (code-sn "(require (lib \"ast.ss\" \"waxeye\") (lib \"fa.ss\" \"waxeye\") (lib \"parser.ss\" \"waxeye\"))")
    (code-psn "(provide " parser-name " (all-from (lib \"ast.ss\" \"waxeye\")))")
    (code-n)
    (code-iu-num
     2
     (code-paren
      (code-sn "define automata")
      (code-i)
      (code-iu
       (code-paren
        (code-s "vector")
        (for-each gen-scheme-fa (vector->list (make-automata grammar))))))
     (code-n)
     (code-n)
     (code-paren
      (code-s "define ")
      (code-s parser-name)
      (code-s " ")
      (code-paren
       (code-s "make-parser")
       (code-s " ")
       (code-s *start-index*)
       (code-s " ")
       (code-s (if *eof-check*
                   "#t"
                   "#f"))
       (code-s " ")
       (code-s (format "~s" *line-counting*))
       (code-s " ")
       (code-s *tab-width*)
       (code-s " ")
       (code-s "automata"))))
    (code-n)
    (code-sn ")")))

)
