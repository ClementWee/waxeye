;;; Waxeye Parser Generator
;;; www.waxeye.org
;;; Copyright (C) 2008 Orlando D. A. R. Hill
;;;
;;; Permission is hereby granted, free of charge, to any person obtaining a copy of
;;; this software and associated documentation files (the "Software"), to deal in
;;; the Software without restriction, including without limitation the rights to
;;; use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies
;;; of the Software, and to permit persons to whom the Software is furnished to do
;;; so, subject to the following conditions:
;;;
;;; The above copyright notice and this permission notice shall be included in all
;;; copies or substantial portions of the Software.
;;;
;;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
;;; IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
;;; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
;;; AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
;;; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
;;; OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
;;; SOFTWARE.


(module
scheme
mzscheme

(require (lib "ast.ss" "waxeye")
         (lib "fa.ss" "waxeye")
         "code.scm" "dfa.scm" "gen.scm" "util.scm" "version.scm")
(provide gen-scheme gen-scheme-parser code-scheme-comment-base)


(define (gen-scheme grammar path)
  (indent-unit! 1)
  (dump-string (gen-scheme-parser grammar) (string-append path (if *name-prefix*
                                                                   (string-append *name-prefix* "-parser.scm")
                                                                   "parser.scm"))))


(define (code-scheme-comment-base unit lines)
  (string-concat (map (lambda (a)
                        (if (equal? "" a)
                            (format "~a\n" unit)
                            (format "~a ~a\n" unit a)))
                      lines)))


(define (code-scheme-header-comment lines)
  (code-scheme-comment-base ";;;" lines))


(define (gen-scheme-trans a)
  (define (gen-list-item a)
    (if (char? a)
        (code-s (format "~s" a))
        (code-paren
         (code-s "cons ")
         (code-s (format "~s" (car a)))
         (code-s " ")
         (code-s (format "~s" (cdr a))))))
  (cond
   ((symbol? a) (code-s (format "'~s" a)))
   ((list? a)
    (code-s "(list ")
    (gen-list-item (car a))
    (for-each (lambda (b)
                (code-s " ")
                (gen-list-item b))
              (cdr a))
    (code-s ")"))
   (else (code-s (format "~s" a)))))


(define (gen-scheme-edge a)
  (code-n)
  (code-i)
  (code-paren
   (code-s "make-edge")
   (code-s " ")
   (gen-scheme-trans (edge-t a))
   (code-s " ")
   (code-s (edge-s a))
   (code-s " ")
   (code-s (edge-v a))))


(define (gen-scheme-edges edges)
  (code-iu
   (code-paren
    (code-s "list")
    (for-each gen-scheme-edge edges)))
  "edges")


(define (gen-scheme-state a)
  (format "\n~a(make-state ~a ~a)"
          (ind)
          (gen-scheme-edges (state-edges a))
          (state-match a)))


(define (gen-scheme-states states)
  (indent (format "(vector~a)" (string-concat (map gen-scheme-state (vector->list states))))))


(define (gen-scheme-fa a)
  (format "\n~a(make-fa '~a ~a '~a)"
          (ind)
          (camel-case-lower (symbol->string (fa-type a)))
          (gen-scheme-states (fa-states a))
          (fa-mode a)))


(define (gen-scheme-parser grammar)
  (let ((parser-name (if *name-prefix*
                         (string-append *name-prefix* "-parser")
                         "parser")))
    (code-iu-num
     2
     (code-n)
     (code-n)
     (code-paren
      (code-s "define ")
      (code-s parser-name)
      (code-s " ")
      (code-paren
       (code-s "make-parser")
       (code-s " ")
       (code-s *start-index*)
       (code-s " ")
       (code-s (if *eof-check*
                   "#t"
                   "#f"))
       (code-s " ")
       (code-s (format "~s" *line-counting*))
       (code-s " ")
       (code-s *tab-width*)
       (code-s " ")
       (code-s "automata"))))
    (code-n)

    (format
#<<EOF
~a
(module
~a
mzscheme

(require (lib \"ast.ss\" \"waxeye\") (lib \"fa.ss\" \"waxeye\") (lib \"parser.ss\" \"waxeye\"))
(provide ~a (all-from (lib \"ast.ss\" \"waxeye\")))

~a
)

EOF

(if *file-header*
    (code-scheme-header-comment *file-header*)
    (code-scheme-header-comment (list (string-append "Generated by the Waxeye Parser Generator - version " *version*) "www.waxeye.org")))

parser-name

parser-name

(indentn 2 (format

#<<EOF
(define automata
~a~a)
EOF

(ind)
(indent (format "(vector~a)" (string-concat (map gen-scheme-fa (vector->list (make-automata grammar))))))))

)))

)
